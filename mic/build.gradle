plugins {
    id "de.undercouch.download" version "3.2.0"
}

apply plugin: 'java'

sourceSets {
    main {
        java {
            srcDirs = ['src/mic'] // Consider moving src/mic to src/main/java
        }
    }
}

repositories {
    jcenter()
}

sourceCompatibility = '1.5'
targetCompatibility = '1.5'

dependencies {
    compileOnly files('lib/vengine.jar')
    compileOnly 'org.slf4j:slf4j-api:1.7.7'
    compile 'commons-codec:commons-codec:1.6'
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.1.5'
    compile group: 'com.google.guava', name: 'guava-jdk5', version: '17.0'
}

task unpackVmod(type: Copy) {
    from zipTree(file('build/distributions/swxwmg.vmod'))
    exclude '**/*.class', 'META-INF/', 'com/google/**/*', 'com/fasterxml/**/*'
    includeEmptyDirs = false
    into file('swxwmg.vmod-unpacked')
}

task downloadXwingData(type: de.undercouch.gradle.tasks.download.Download) {
    ext.destDir = 'swxwmg.vmod-unpacked'
    src([
            'https://raw.githubusercontent.com/guidokessels/xwing-data/master/data/pilots.js',
            'https://raw.githubusercontent.com/guidokessels/xwing-data/master/data/ships.js',
            'https://raw.githubusercontent.com/guidokessels/xwing-data/master/data/upgrades.js'
    ])
    dest destDir
    doLast {
        for (f in ['pilots', 'ships', 'upgrades']) {
            file("${destDir}/${f}.json").delete();
            file("${destDir}/${f}.js").renameTo(file("${destDir}/${f}.json"))
        }
    }
}

task buildVmod(type: Zip) {
    archiveName 'swxwmg.vmod'
    duplicatesStrategy 'exclude'
    from 'swxwmg.vmod-unpacked'
    from compileJava
    for (dependency in (configurations.compile).files) {
        // Need to extract jars so vassal can find classes
        from zipTree(dependency)
    }
    exclude "**/*.jar"
    doLast {
        println "Compiled vmod at ${archivePath}"
    }
}
